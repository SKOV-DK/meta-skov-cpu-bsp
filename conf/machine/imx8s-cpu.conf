#@TYPE: Machine
#@NAME: imx8s-cpu
#@DESCRIPTION: Machine configuration for Skov's imx8s-cpu (i.MX8MPlus) secure boot

SOC_FAMILY = "mx8m-generic-bsp"
require conf/machine/include/soc-family.inc

DEFAULTTUNE = "cortexa53-crypto"
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

MACHINE_FEATURES = "usbgadget usbhost vfat touchscreen serial rtc habv4"
MACHINE_EXTRA_RRECOMMENDS = " \
    kernel-modules \
"

# Inactivate the recommendation weakly set in poky's kernel.bbclass [1] to get
# rid of kernel images in the root-FS (/boot/*Image*) as they are separately
# stored via FIT-images in dedicated partitions.
#
# [1] https://git.yoctoproject.org/poky/tree/meta/classes-recipe/kernel.bbclass?h=mickledore-4.2.3&id=aa63b25cbe25#n681
BAD_RECOMMENDATIONS = "kernel-image-*"

MACHINE_SOCARCH = "${TUNE_PKGARCH}-mx8mp"
MACHINE_ARCH_FILTER = "virtual/kernel"
PACKAGE_EXTRA_ARCHS:append = " ${MACHINE_SOCARCH}"

EXTRA_IMAGEDEPENDS += "virtual/bootloader"

PREFERRED_PROVIDER_virtual/bootloader ?= "barebox"
BAREBOX_IMAGE = "barebox-skov-imx8mp-s.img"
BAREBOX_PADDING_SKIP = "32768"
BAREBOX_PADDING_OFFSET = "32"

PREFERRED_PROVIDER_virtual/egl:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgl:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles1:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles2:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles3:mx8 ?= "mesa"
SERIAL_CONSOLES = "115200;ttymxc1"

PREFERRED_PROVIDER_virtual/wpebackend ?= "wpebackend-fdo"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-skov"

PREFERRED_PROVIDER_virtual/java-native ?= "cacao-native"
PREFERRED_PROVIDER_virtual/java-initial-native ?= "cacao-initial-native"

PREFERRED_PROVIDER_virtual/rauc-signing ?= "ptx-dev-keys-native"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = " \
    freescale/imx8mp-skov-basic.dtb \
    freescale/imx8mp-skov-revb-hdmi.dtb \
    freescale/imx8mp-skov-revb-lt6.dtb \
    freescale/imx8mp-skov-revb-mi1010ait-1cp1.dtb \
    freescale/imx8mp-skov-revc-bd500.dtb \
    freescale/imx8mp-skov-revc-jutouch-jt070tm041.dtb \
    freescale/imx8mp-skov-revc-jutouch-jt101tm023.dtb \
"

# OP-TEE shall be placed 32 MiB before the end of the RAM. According to the
# boards devicetree the RAM starts at 0x40000000 which for 2 GiB of memory
# results in: 0x40000000 + 2 GiB - 32 MiB = 0xbe000000. Choose 2 MiB for
# OP-TEE's shared memory area so that 30 MiB remain for OP-TEE's core.
OPTEE_LOADADDR = "0xbe000000"
OPTEE_CORE_SIZE = "0x01e00000"
OPTEE_SHMEM_SIZE = "0x00200000"

DDR_FIRMWARE_NAME = " \
	lpddr4_pmu_train_1d_imem.bin \
	lpddr4_pmu_train_1d_dmem.bin \
	lpddr4_pmu_train_2d_imem.bin \
	lpddr4_pmu_train_2d_dmem.bin \
"

IMAGE_FSTYPES += "tar.bz2 ext4"

# Choose defaults in the context of dm-verity protected root-FSs
VERITY_IMAGE_FSTYPE = "ext4"
VERITY_IMAGE_SUFFIX = ".verity"

# Our image recipes utilizing wic are parameterized in a way that allows to
# choose if the produced wic-image is suited for installation onto the eMMC or
# onto an SD-card (additionally to the image layout the fstab entry for /home is
# adjusted accordingly). It is set to "eMMC" per default here and can be
# overwritten to be "SD" in your local.conf or from the bitbake-commandline via
#   $ env TARGET_MEDIUM="SD" BB_ENV_PASSTHROUGH_ADDITIONS="$BB_ENV_PASSTHROUGH_ADDITIONS TARGET_MEDIUM" bitbake [...]
TARGET_MEDIUM ?= "eMMC"

PREFERRED_RPROVIDER_ssh ?= "openssh"
