#@TYPE: Machine
#@NAME: imx8-cpu
#@DESCRIPTION: Machine configuration for Skov's imx8-cpu (i.MX8MPlus)

SOC_FAMILY = "mx8m-generic-bsp"
require conf/machine/include/soc-family.inc

DEFAULTTUNE = "cortexa53-crypto"
require conf/machine/include/arm/armv8a/tune-cortexa53.inc

MACHINEOVERRIDES .= ":imx8-cpu"
MACHINE_FEATURES = "usbgadget usbhost vfat touchscreen serial rtc"
MACHINE_EXTRA_RRECOMMENDS = " \
    kernel-modules \
    kernel-devicetree \
"

MACHINE_SOCARCH = "${TUNE_PKGARCH}-mx8mp"
MACHINE_ARCH_FILTER = "virtual/kernel"
PACKAGE_EXTRA_ARCHS:append = " ${MACHINE_SOCARCH}"

EXTRA_IMAGEDEPENDS += "virtual/bootloader"

PREFERRED_PROVIDER_virtual/bootloader ?= "barebox"
BAREBOX_IMAGE = "barebox-skov-imx8mp-s.img"
BAREBOX_PADDING_SKIP = "32768"
BAREBOX_PADDING_OFFSET = "32"

PREFERRED_PROVIDER_virtual/egl:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgl:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles1:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles2:mx8 ?= "mesa"
PREFERRED_PROVIDER_virtual/libgles3:mx8 ?= "mesa"
SERIAL_CONSOLES = "115200;ttymxc1"

PREFERRED_PROVIDER_virtual/wpebackend ?= "wpebackend-fdo"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-skov"

PREFERRED_PROVIDER_virtual/java-native ?= "cacao-native"
PREFERRED_PROVIDER_virtual/java-initial-native ?= "cacao-initial-native"

PREFERRED_PROVIDER_virtual/rauc-signing ?= "ptx-dev-keys-native"

KERNEL_IMAGETYPE = "Image"
KERNEL_DEVICETREE = " \
    freescale/imx8mp-skov-basic.dtb \
    freescale/imx8mp-skov-revb-hdmi.dtb \
    freescale/imx8mp-skov-revb-lt6.dtb \
    freescale/imx8mp-skov-revb-mi1010ait-1cp1.dtb \
    freescale/imx8mp-skov-revc-bd500.dtb \
    freescale/imx8mp-skov-revc-tian-g07017.dtb \
"

DDR_FIRMWARE_NAME = " \
	lpddr4_pmu_train_1d_imem.bin \
	lpddr4_pmu_train_1d_dmem.bin \
	lpddr4_pmu_train_2d_imem.bin \
	lpddr4_pmu_train_2d_dmem.bin \
"

IMAGE_FSTYPES += "tar.bz2 ext4"

# Our image recipes utilizing wic are parameterized in a way that allows to
# choose if the produced wic-image is suited for installation onto the eMMC or
# onto an SD-card (additionally to the image layout the fstab entry for /home is
# adjusted accordingly). It is set to "eMMC" per default here and can be
# overwritten to be "SD" in your local.conf or from the bitbake-commandline via
#   $ env TARGET_MEDIUM="SD" BB_ENV_PASSTHROUGH_ADDITIONS="$BB_ENV_PASSTHROUGH_ADDITIONS TARGET_MEDIUM" bitbake [...]
TARGET_MEDIUM ?= "eMMC"

# The factory install script needs to know which image shall be installed. It is
# set to "production" per default here and can be overwritten to be e.g.
# "pmc-dol" in your local.conf or from the bitbake-commandline via
#   $ env FACTORY_INSTALL_IMAGE="pmc-dol" BB_ENV_PASSTHROUGH_ADDITIONS="$BB_ENV_PASSTHROUGH_ADDITIONS FACTORY_INSTALL_IMAGE" bitbake [...]
FACTORY_INSTALL_IMAGE ?= "production"

PREFERRED_RPROVIDER_ssh ?= "openssh"
